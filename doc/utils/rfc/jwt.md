[ยง2] (rfc jwt) - Json Web Token {#rfc.jwt}
-------------

###### [!Library] `(rfc jwt)` 

This library provides Json Web Token (JWT) APIs. JWT is defined
in [RFC 7519](https://tools.ietf.org/html/rfc7519).


The following example shows how to consume JWT

``````````scheme
(import (rnrs)
        (rfc jwt)
        (rfc jwk)
        (rfc jwe)
        (rfc jws)
        (rfc uuid)
        (crypto)
        (srfi :19)
        (sagittarius combinators))

(define keypair (generate-key-pair Ed25519))
(define alg 'EdDSA)
;; (define keypair (generate-key-pair ECDSA :ec-parameter NIST-P-256))
;; (define alg 'ES256)
;; (define keypair (generate-key-pair RSA :size 2048))
;; (define alg 'PS512)

(define claims
  (jwt-claims-builder
   (iss "Sagittarius Scheme")
   (aud "All saints")
   (sub "Use Sagittarius")
   (iat (current-time))
   (nbf (add-duration (current-time) (make-time time-duration 0 -1)))
   (exp (add-duration (current-time) (make-time time-duration 0 600)))
   (jti (uuid->string (make-v4-uuid)))))

(define jws-header
  (jws-header-builder
   (alg alg)))

(define payload (string->utf8 (jwt-claims->json-string claims)))
(define jws-object (make-jws-object jws-header payload))

(define signer (private-key->jws-signer (keypair-private keypair)))

(define jwk
  (public-key->jwk (keypair-public keypair)
                   (jwk-config-builder (kid "my key"))))
(define jwks (make-jwk-set (list jwk)))

(let ((jwt-object (jws:sign jws-object signer)))
  ;; Share the JWT to 3rd party
  ;; (jws:serialize jwt-object)
  ;; (jwk-set->json-string jwks)

  ;; Verify the JWT token with the public key
  (let* ((kid-matcher (jwk-matcher:kid "my key"))
         (verifier (public-key->jws-verifier
                    (jwk-set:find-key jwks kid-matcher)))
         (jwt-consumer (jwt-consumer-builder
                        (verifier verifier)
                        (claims-validator
                         ;; Validate claims
                         (compose jwt:iss-required-validator
                                  jwt:sub-required-validator
                                  jwt:aud-required-validator
                                  jwt:exp-required-validator
                                  jwt:nbf-required-validator
                                  jwt:iat-required-validator
                                  jwt:jti-required-validator
                                  (jwt:iss-value-validator "Sagittarius Scheme"
                                                           "Sagittarius")
                                  (jwt:sub-value-validator "Use Sagittarius")
                                  (jwt:aud-value-validator "All saints")
                                  (jwt:nbf-validator)
                                  (jwt:exp-validator)))))
         (claims (jwt:consume jwt-consumer jwt-object)))
    ;; use the user claim
    (jwt-claims-aud claims))) ;; retrieve 'aud' field
``````````

### [ยง3] JWT

By definition, JWT is either JWE or JWS. To create JWT, users need to
use either `(rfc jwe)` or  `(rfc jws)` library. For more details,
please refer the respective section of the document.

###### [!Function] `jwt:consume`  _jwt-consumer_ _jwt-string/object_

Returns JWT claims after decrypt and/or verify the given
_jwt-string/object_.

_jwt-consumer_ must be a JWT consumer object.

_jwt-string/object_ must be either a string of valid JWT format or
JWE/JWS object.

### [ยง3] JWT Consumer

JWT consumer is an object decrypts and/or verifies the JWT and validates the
claims. JWT consumer contains the below fields:

- `decryptor`: JWE decryptor
- `verifier`: JWS verifier
- `claims-validator`: Claims validator, see below section for more details

###### [!Function] `jwt-consumer?`  _obj_

Returns #t if the given _obj_ is a JWT consumer otherwise #f.

###### [!Macro] `jwt-consumer-builder` 

A builder macro of JWT consumer. The macro is generated by
`(record builder)`. see [(record builder)](#record.builder)for more details.

JWT consumer execute validation if the `claims-validator` field is
set. The field must hold claims validator, which is a procedure takes
one argument, _claims_, and returns the given _claims_ or raises
an error if the validation failed.

###### [!Function] `jwt:iss-required-validator`  _claims_
###### [!Function] `jwt:sub-required-validator`  _claims_
###### [!Function] `jwt:aud-required-validator`  _claims_
###### [!Function] `jwt:exp-required-validator`  _claims_
###### [!Function] `jwt:nbf-required-validator`  _claims_
###### [!Function] `jwt:iat-required-validator`  _claims_
###### [!Function] `jwt:jti-required-validator`  _claims_

Claims validator to validate required field of the given _claims_.

###### [!Function] `jwt:iss-validator`  _iss_ _..._
###### [!Function] `jwt:aud-validator`  _aud_ _..._
###### [!Function] `jwt:sub-validator`  _sub_

Creates claims validator to validate `iss`, `aud` and
`sub`, respectively.

The first 2 procedures may take multiple argument and checks if one of the
value matches or not.


###### [!Function] `jwt:exp-validator` 
###### [!Function] `jwt:exp-validator`  _clock-skew_
###### [!Function] `jwt:exp-validator`  _clock-skew_ _future-bound-seconds_

Creates claims validator to validate _exp_ field is
expired or nor.

_clock-skew_ must be an integer represents seconds of clock skew.

_future-bound-seconds_ must be an integer represents seconds of maximum
boundary that `exp` can be. (e.g. `exp` can't be more than 10 mins
of future)


###### [!Function] `jwt:nbf-validator` 
###### [!Function] `jwt:nbf-validator`  _clock-skew_

Creates claims validator to validate if the current time
is earlier than _nbf_ field.

_clock-skew_ must be an integer represents seconds of clock skew.


###### [!Function] `jwt:iat-validator`  _not-before_ _not-after_
###### [!Function] `jwt:iat-validator`  _not-before_ _not-after_ _clock-skew_

Creates claims validator to validate if the `iat` is in between
_not-before_ and _not-after__not-before_ and _not-after_ must be a time object.

_clock-skew_ must be an integer represents seconds of clock skew.


### [ยง3] JWT Claims

JWT claims is an object which contains claims of the JWT.

###### [!Record Type] `<jwt-claims>` 

Record type of JWT claims object.

The object contains the following fields:

- `iss`: Issuer
- `sub`: Subject
- `aud`: Audience
- `exp`: Expiration time
- `nbf`: Not before
- `iat`: Issued at
- `jti`: JWT ID
- `custom-claims`: User defined claims, hashtable



###### [!Function] `jwt-claims?`  _obj_

Returns #t if the _obj_ is JWT claims otherwise #f.

###### [!Macro] `jwt-claims-builder` 

A builder macro of JWT claims. The macro is generated by
`(record builder)`. see [(record builder)](#record.builder)for more details.

###### [!Function] `jwt-claims-iss`  _jwt-claims_
###### [!Function] `jwt-claims-sub`  _jwt-claims_
###### [!Function] `jwt-claims-aud`  _jwt-claims_
###### [!Function] `jwt-claims-exp`  _jwt-claims_
###### [!Function] `jwt-claims-nbf`  _jwt-claims_
###### [!Function] `jwt-claims-iat`  _jwt-claims_
###### [!Function] `jwt-claims-jti`  _jwt-claims_
###### [!Function] `jwt-claims-custom-claims`  _jwt-claims_

Retrieves the field value of _jwt-claims_.

###### [!Function] `json->jwt-claims`  _obj_
###### [!Function] `read-jwt-claims` 
###### [!Function] `read-jwt-claims`  _:optional_ _port_
###### [!Function] `json-string->jwt-claims`  _string_

Construct JWT claims from S-exp JSON representation of _obj_,
from input port _port_ or a string _string_.

If the first form of `read-jwt-claims` is used, then it reads from
current input port.


###### [!Function] `jwt-claims->json`  _jwt-claims_
###### [!Function] `write-jwt-claims`  _jwt-claims_
###### [!Function] `write-jwt-claims`  _jwt-claims_ _port_
###### [!Function] `jwt-claims->json-string`  _jwt-claims_

Serialize the given _jwt-claims_ to a S-exp representaion,
to _port_ or string.

If first form of `write-jwt-claims` is used, then it writes the
serialized JWT claims to current output port.


